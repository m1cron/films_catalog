buildscript {
    ext {
        ver = [
                lombok                  : '1.18.20',
                validationApi           : '2.0.1.Final',
                swagger2                : '2.9.2',
                swaggerAnnotations      : '2.1.1',
                logback                 : '1.2.3',
                logstashLogbackEncoder  : '6.2',
                javaxServlet            : '4.0.1',
                jjwt                    : '0.9.1',
                mapstruct               : '1.4.2.Final',
                hvalidator              : '6.0.2.Final',
                h2                      : '1.4.200',
        ]
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    id 'org.springframework.boot' version '2.4.12'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'ru.micron'
version = '1.0'
rootProject.wrapper.gradleVersion = '7.2'
repositories.addAll(rootProject.buildscript.repositories)
targetCompatibility = sourceCompatibility = JavaVersion.VERSION_11
defaultTasks 'clean', 'build'

dependencies {
    // Spring
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-web"

    // Swagger
    implementation "io.springfox:springfox-swagger2:$ver.swagger2"
    implementation "io.springfox:springfox-swagger-ui:$ver.swagger2"
    implementation "io.swagger.core.v3:swagger-annotations:$ver.swaggerAnnotations"

    // Validation
    implementation "org.hibernate.validator:hibernate-validator:$ver.hvalidator"
    implementation "org.hibernate.validator:hibernate-validator-annotation-processor:$ver.hvalidator"
    implementation "javax.validation:validation-api:$ver.validationApi"

    // Database
    implementation 'org.postgresql:postgresql'
    implementation 'org.liquibase:liquibase-core'

    // JWT
    implementation "io.jsonwebtoken:jjwt:$ver.jjwt"

    // Mapstruct
    implementation "org.mapstruct:mapstruct:$ver.mapstruct"
    annotationProcessor "org.mapstruct:mapstruct-processor:$ver.mapstruct"

    // Servlet
    compileOnly "javax.servlet:javax.servlet-api:4.0.1"

    // Logback
    implementation "ch.qos.logback:logback-classic:$ver.logback"
    implementation "net.logstash.logback:logstash-logback-encoder:$ver.logstashLogbackEncoder"

    // Lombok
    compileOnly "org.projectlombok:lombok:$ver.lombok"
    annotationProcessor "org.projectlombok:lombok:$ver.lombok"

    // Test
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "com.h2database:h2:$ver.h2"
}

test {
    useJUnitPlatform()
}
